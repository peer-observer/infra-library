{
  config,
  lib,
  pkgs,
  ...
}:

with lib;

let
  CONSTANTS = import ../constants.nix;
  baseOptions = (import ../base/base.nix { inherit config lib pkgs; }).options;
  NATS_PORT = 4222;
in
{
  imports = [ ./logrotate.nix ];

  options = {
    enable = lib.mkEnableOption "this host is a Bitcoin node";

    inherit (baseOptions)
      id
      name
      description
      wireguard
      setup
      arch
      extraConfig
      extraModules
      ;

    bitcoind = {
      prune = lib.mkOption {
        type = lib.types.int;
        default = 4000;
        description = "The prune parameter for Bitcoin Core. 0 turns pruning off.";
      };

      package = lib.mkOption {
        type = lib.types.package;
        # this is the default package without overrides
        default = (pkgs.callPackage ../../pkgs/bitcoind/default.nix { }) { };
        description = "The bitcoind package to run on this node.";
      };

      chain = lib.mkOption {
        type = types.enum [
          "main"
          "test"
          "testnet4"
          "signet"
          "regtest"
        ];
        default = "main";
        description = "The chain / network the node should run.";
      };

      customPort = lib.mkOption {
        type = types.nullOr types.port;
        default = null;
        description = "A custom port the Bitcoin node should use for the P2P network. If unset, the default port is used.";
        example = 12345;
      };

      extraConfig = lib.mkOption {
        type = lib.types.str;
        default = "";
        description = "Extra configuration passed to bitcoind in the 'bitcoin.conf' format.";
      };

      net = {
        useASMap = lib.mkEnableOption "using a recent ASMap file with this node. See https://asmap.org for more information.";

        useTor = lib.mkEnableOption "Tor with this node and accept connections from Tor.";

        useI2P = lib.mkEnableOption "i2p with this node and accept connections from i2p.";

        useCJDNS = lib.mkEnableOption "CJDNS with this node and accept connections from CJDNS.";
      };

      dataDir = lib.mkOption {
        type = lib.types.nullOr lib.types.str;
        default = null;
        description = "The data directory of the node. By default, this is /var/lib/bitcoind-*/. Setting this can be useful if there's a bigger drive mounted somewhere else.";
      };

      banlistScript = lib.mkOption {
        type = lib.types.nullOr lib.types.str;
        description = "A banlist script. Has access to the 'RPC_BAN_USER' and 'RPC_BAN_PW' env variables";
        default = null;
      };

      detailedLogging = {
        enable = lib.mkOption {
          type = lib.types.bool;
          description = "If enabled, turn on potentially spammy debug log categories like `net` and `mempoolrej`. The debug.log is rotated hourly and each compressed hourly chunk is accumulated into a per-day gzip archive kept next to debug.log in the bitcoind data dir; a day's archive is only published to the served/rcloned debug-logs dir once the day is complete (the next day), so half-finished days are never exposed.";
          default = true;
        };
        logsToKeep = lib.mkOption {
          type = lib.types.ints.u16;
          description = "Number of finalized daily debug.log archives to keep on the server before deleting them. The log is rotated hourly and published as one gzip file per completed day, so this is a count of days.";
          default = 4;
          example = 2;
        };
        printToConsole = lib.mkEnableOption "Wether to print the debug logs to console (i.e. systemd) too. Disabled by default, but can be useful for testing.";
      };

      addrmanSnapshots = {
        enable =
          lib.mkEnableOption "periodic addrman snapshots using getrawaddrman. Snapshots are compressed with zstd and stored daily."
          // {
            default = true;
          };
        snapshotsToKeep = lib.mkOption {
          type = lib.types.ints.u16;
          description = "Number of daily snapshots to keep on the server before deleting them.";
          default = 30;
          example = 7;
        };
      };

      peersDatSnapshots = {
        enable =
          lib.mkEnableOption "periodic snapshots of the node's peers.dat file. Snapshots are compressed with zstd and stored weekly."
          // {
            default = true;
          };
        snapshotsToKeep = lib.mkOption {
          type = lib.types.ints.u16;
          description = "Number of weekly snapshots to keep on the server before deleting them.";
          default = 26;
          example = 4;
        };
      };

      peerinfoSnapshots = {
        enable =
          lib.mkEnableOption "periodic snapshots of the node's peers using getpeerinfo. Snapshots are compressed with zstd and stored every six hours."
          // {
            default = true;
          };
        daysToKeep = lib.mkOption {
          type = lib.types.ints.u16;
          description = "Number of days of snapshots to keep on the server before deleting them. Snapshots are taken every six hours, so a day is four snapshots.";
          default = 30;
          example = 7;
        };
      };
    };

    fork-observer = {
      enable = lib.mkEnableOption "this node for use in a fork-observer instance." // {
        default = true;
      };
    };

    addrman-observer = {
      enable = lib.mkEnableOption "this node for use in a addrman-observer instance." // {
        default = true;
      };
    };

    peer-observer = {
      extractors = {
        logs = {
          enable = lib.mkEnableOption "the peer-observer log-extractor" // {
            default = true;
          };
        };
        ipc = {
          enable = lib.mkEnableOption "the peer-observer ipc-extractor" // {
            default = true;
          };
        };
      };
      tools = {
        archiver = {
          enable = lib.mkEnableOption "the peer-observer archiver";

          baseName = mkOption {
            type = types.str;
            default = null;
            example = "demo-peer-observer";
            description = "Base name for archive files (e.g., 'mainnet' -> '[node-name]-mainnet.<timestamp>.bin.zst')";
          };

          compressionLevel = mkOption {
            type = types.ints.between 0 22;
            default = 9;
            example = 3;
            description = "Zstd compression level (0 = no compression, 1-22)";
          };

        };
      };

      addrLookup = lib.mkEnableOption "the peer-observer address-connectivity lookup tool. This reaches out to nodes on the network and might leak IP addresses.";

    };

    samply-continuous-profiling = lib.mkEnableOption "samply continuous CPU profiling of the bitcoind process. Writes one 24h-long, gzipped sample file per run to /data/profiling, restarting on exit. Runs as root to ptrace bitcoind.";
  };

  config = lib.mkIf (config.peer-observer.node.enable && !config.peer-observer.base.setup) {

    # A NATS server that the peer-observer and
    services.nats = {
      enable = true;
      settings = {
        listen = "127.0.0.1:${toString NATS_PORT}";
        http = "127.0.0.1:8222";
        # See https://github.com/0xB10C/peer-observer/blob/master/extractors/ebpf/README.md#nats-settings
        # addtionally, some RPC events like `getrawaddrman` produce a large message.
        max_payload = 15728640; # 15MB
      };
    };

    # HACK: restart peer-observer-tool-metrics every 48h
    # TODO: fix peer-observer metrics to not grow on RAM too much.
    # systemd.services."peer-observer-tool-metrics".serviceConfig.RuntimeMaxSec = "48h";

    # To be sure to notice a crashed Bitcoin node, we configure the NixOS systemd service to
    # NOT restart the node. This must be done manually (either systemctl restart bitcoind-mainnet
    # or restarting the whole host). Calling the "stop" RPC also causes the node to be NOT restarted.
    # See https://www.freedesktop.org/software/systemd/man/latest/systemd.service.html#Restart=
    systemd.services.bitcoind-mainnet.serviceConfig.Restart = lib.mkForce "no";
    systemd.services.bitcoind-mainnet.serviceConfig.LimitCORE = mkIf (
      config.peer-observer.node.bitcoind.package.sanitizersAddressUndefined
      || config.peer-observer.node.bitcoind.package.sanitizersThread
    ) "infinity";

    # Raise systemd-coredump limits for sanitizer builds so dumps aren't
    # silently truncated. Core dumps land in /var/lib/systemd/coredump/ and
    # are accessible via `coredumpctl list / coredumpctl dump`.
    systemd.coredump.settings.Coredump =
      mkIf
        (
          config.peer-observer.node.bitcoind.package.sanitizersAddressUndefined
          || config.peer-observer.node.bitcoind.package.sanitizersThread
        )
        {
          ExternalSizeMax = "infinity";
          ProcessSizeMax = "infinity";
        };

    systemd.services.bitcoind-mainnet.environment = mkMerge [
      (mkIf config.peer-observer.node.bitcoind.package.sanitizersAddressUndefined {
        ASAN_OPTIONS = "detect_leaks=1:abort_on_error=1:halt_on_error=1";
        LSAN_OPTIONS = "suppressions=${config.peer-observer.node.bitcoind.package.sanitizerSuppressionsDir}/lsan:print_stacktrace=1:halt_on_error=1";
        UBSAN_OPTIONS = "suppressions=${config.peer-observer.node.bitcoind.package.sanitizerSuppressionsDir}/ubsan:print_stacktrace=1:halt_on_error=1";
      })
      (mkIf config.peer-observer.node.bitcoind.package.sanitizersThread {
        TSAN_OPTIONS = "suppressions=${config.peer-observer.node.bitcoind.package.sanitizerSuppressionsDir}/tsan:print_stacktrace=1:halt_on_error=1:second_deadlock_stack=1:verbosity=1";
      })
    ];

    # for backwards compatability reasons, this is called "mainnet" and has to stay this way for now.
    # Even if we run a signet/testnet/regtest node..
    services.bitcoind."mainnet" = {
      enable = true;
      package = config.peer-observer.node.bitcoind.package;
      dataDir = mkIf (
        config.peer-observer.node.bitcoind.dataDir != null
      ) config.peer-observer.node.bitcoind.dataDir;
      rpc.port = CONSTANTS.BITCOIND_RPC_PORT;
      prune = config.peer-observer.node.bitcoind.prune;
      # needs to be "/run/bitcoind-<name>/bitcoind.pid"
      pidFile = "/run/bitcoind-mainnet/bitcoind.pid";
      extraConfig = ''

        chain=${config.peer-observer.node.bitcoind.chain}
        [${config.peer-observer.node.bitcoind.chain}]

        port=${
          toString (
            if config.peer-observer.node.bitcoind.customPort != null then
              config.peer-observer.node.bitcoind.customPort
            else
              CONSTANTS.BITCOIND_P2P_PORT_BY_CHAIN."${config.peer-observer.node.bitcoind.chain}"
          )
        }

        # disabled, as it has gotten quite spammy in
        # https://github.com/bitcoin/bitcoin/pull/32604/commits/d541409a64c60d127ff912dad9dea949d45dbd8c
        logsourcelocations=0
        logips=1
        logthreadnames=1
        logtimemicros=1
        server=1
        rpcbind=127.0.0.1
        rpcallowip=127.0.0.1
        rpcwhitelistdefault=0

        ${optionalString (config.services.peer-observer.extractors.p2p.enable) ''
          addnode=${config.services.peer-observer.extractors.p2p.p2pAddress}
        ''}
        ${optionalString (config.services.peer-observer.extractors.ipc.enable) ''
          ipcbind=unix:/run/bitcoind-mainnet/node.sock
        ''}
        ${optionalString (config.services.peer-observer.extractors.rpc.enable) ''
          server=1

          # rpc-extractor (peer-observer) user
          rpcwhitelist=rpc-extractor:getpeerinfo,getmempoolinfo,uptime,getnettotals,getaddrmaninfo,getmemoryinfo,getchaintxstats,getnetworkinfo,getblockchaininfo,getorphantxs,getrawaddrman,estimatesmartfee
          rpcauth=rpc-extractor:${CONSTANTS.RPC_EXTRACTOR_RPC_AUTH}
        ''}
        ${optionalString config.peer-observer.node.bitcoind.addrmanSnapshots.enable ''
          server=1

          # addrman-snapshot user
          rpcwhitelist=addrman-snapshot:getrawaddrman
          rpcauth=addrman-snapshot:${CONSTANTS.ADDRMAN_SNAPSHOTS_RPC_AUTH}
        ''}
        ${optionalString config.peer-observer.node.bitcoind.peerinfoSnapshots.enable ''
          server=1

          # peerinfo-snapshot user
          rpcwhitelist=peerinfo-snapshot:getpeerinfo
          rpcauth=peerinfo-snapshot:${CONSTANTS.PEERINFO_SNAPSHOTS_RPC_AUTH}
        ''}
        ${optionalString (config.peer-observer.node.bitcoind.banlistScript != null) ''
          server=1

          # banlist user
          rpcwhitelist=ban:clearbanned,listbanned,setban
          rpcauth=ban:${CONSTANTS.BANLIST_RPC_RPC_AUTH}
        ''}
        ${optionalString (config.peer-observer.node.fork-observer.enable) ''
          server=1
          rest=1

          # forkobserver user
          # Don't allow getnetworkinfo even if fork-observer tires to use it to fetch the version
          # Otherwise, fork-observer will show the version, which can be a honeypot leak.
          rpcwhitelist=forkobserver:getchaintips,getblockhash,getblockheader,getblock,waitfornewblock
          rpcauth=forkobserver:${CONSTANTS.FORK_OBSERVER_RPC_AUTH}
        ''}
        ${optionalString (config.peer-observer.node.addrman-observer.enable) ''
          server=1

          # addrmanobserver user
          rpcwhitelist=addrmanobserver:getrawaddrman
          rpcauth=addrmanobserver:${CONSTANTS.ADDRMAN_OBSERVER_RPC_AUTH}
        ''}
        ${optionalString config.peer-observer.node.bitcoind.net.useTor "debug=tor"}
        ${optionalString config.peer-observer.node.bitcoind.net.useI2P ''
          debug=i2p
          i2pacceptincoming=1
          i2psam=${config.services.i2pd.proto.sam.address}:${toString config.services.i2pd.proto.sam.port}
        ''}
        ${optionalString config.peer-observer.node.bitcoind.net.useCJDNS ''
          cjdnsreachable=1
        ''}
        ${optionalString config.peer-observer.node.bitcoind.net.useASMap ''
          asmap=${config.peer-observer.base.b10c-pkgs.asmap-data}
        ''}
        ${optionalString config.peer-observer.node.bitcoind.detailedLogging.enable ''
          ${lib.concatStrings (map (cat: "debug=${cat}\n") CONSTANTS.DETAILED_DEBUG_LOG_CATEGORIES)}
          printtoconsole=${
            if config.peer-observer.node.bitcoind.detailedLogging.printToConsole then "1" else "0"
          }
        ''}
      ''
      + config.peer-observer.node.bitcoind.extraConfig;
    };

    networking = {
      wireguard.interfaces.${CONSTANTS.WIREGUARD_INTERFACE_NAME} = {
        # the remainder of this interface is defined in base.nix

        # node hosts are connected to web hosts. The node hosts initiate
        # the wireguard connection to the web hosts, so node hosts don't
        # need to be reachable from the public internet. Web hosts need
        # to be reachable.
        listenPort = null;
        peers = map (webserver: {
          allowedIPs = [ "${webserver.wireguard.ip}/32" ];
          endpoint = "${webserver.domain}:${toString CONSTANTS.WIREGUARD_INTERFACE_PORT}";
          publicKey = webserver.wireguard.pubkey;
          persistentKeepalive = 25;
        }) (lib.attrValues (lib.attrsets.filterAttrs (name: host: !host.setup) config.infra.webservers));
      };
      firewall = {
        allowedTCPPorts = [
          (
            if config.peer-observer.node.bitcoind.customPort != null then
              config.peer-observer.node.bitcoind.customPort
            else
              CONSTANTS.BITCOIND_P2P_PORT_BY_CHAIN."${config.peer-observer.node.bitcoind.chain}"
          )
        ];
        interfaces.${CONSTANTS.WIREGUARD_INTERFACE_NAME}.allowedTCPPorts = [
          # A nginx that proxies (and compresses, if possible) the connections between the webserver and node.
          CONSTANTS.NODE_TO_WEBSERVER_PORT
        ];
      };
    };

    services.tor = {
      enable = config.peer-observer.node.bitcoind.net.useTor;
      client.enable = true;
      settings.ControlPort = [ { port = 9051; } ];
    };

    services.i2pd = {
      enable = config.peer-observer.node.bitcoind.net.useI2P;
      proto.sam.enable = true;
    };

    services.cjdns = {
      enable = config.peer-observer.node.bitcoind.net.useCJDNS;
      UDPInterface = {
        bind = "0.0.0.0:36468";
        # These are CJNDS peers I found to be working. However,
        # they might not be needed anymore with newer CJDNS versions..
        # due to better peer finding. If that's true, these can be removed.
        connectTo = {
          "107.170.57.34:63472" = {
            login = "public-peer";
            password = "ppm6j89mgvss7uvtntcd9scy6166mwb";
            peerName = "cord.ventricle.us";
            publicKey = "1xkf13m9r9h502yuffsq1cg13s5648bpxrtf2c3xcq1mlj893s90.k";
          };
          "81.6.2.165:56879" = {
            login = "theswissbay-peering-login";
            password = "rr1lsx8vvxq7m5107gvsn98gc2h2l54";
            peerName = "theswissbay.ch";
            publicKey = "nuvtkly8swgkwsyyjrv89f4y4y0w3x17w61twgsfh9zv1r87h060.k";
          };
        };
      };
    };

    assertions = [
      {
        assertion =
          config.peer-observer.node.peer-observer.extractors.ipc.enable
          == config.peer-observer.node.bitcoind.package.symlinkBitcoinNode;
        message = "ipc-extractor enabled, but bitcoin-node not symlinked on ${config.peer-observer.base.name}";
      }
    ];

    services.peer-observer = {
      extractors = {
        dependOn = "bitcoind-mainnet";
        ebpf = {
          enable = true;
          bitcoindPIDFile = config.services.bitcoind.mainnet.pidFile;
          bitcoindPath = "${config.services.bitcoind.mainnet.package}/bin/bitcoind";
        };
        rpc = {
          enable = true;
          rpcHost = "127.0.0.1:${toString config.services.bitcoind.mainnet.rpc.port}";
          rpcUser = "rpc-extractor";
          rpcPass = CONSTANTS.RPC_EXTRACTOR_RPC_PASSWORD;
          metricsAddress = "127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_RPC_METRICS_PORT}";
        };
        p2p = {
          enable = true;
          p2pAddress = "127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_EXTRACTOR_P2P_PORT}";
          network =
            CONSTANTS.PEER_OBSERVER_EXTRACTOR_P2P_NETWORK_NAME_MAP."${config.peer-observer.node.bitcoind.chain
            }";
        };
        log = {
          enable = config.peer-observer.node.peer-observer.extractors.logs.enable;
          debugLog = "/var/lib/bitcoind-mainnet/debug.log";
        };
        ipc = {
          enable = config.peer-observer.node.peer-observer.extractors.ipc.enable;
          metricsAddress = "127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_IPC_METRICS_PORT}";
          ipcSocket = "/run/bitcoind-mainnet/node.sock";
        };
      };

      tools = {
        metrics = {
          enable = true;
          metricsAddress = "127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_METRICS_PORT}";
        };

        addrConnectivity = {
          enable = config.peer-observer.node.peer-observer.addrLookup;
          metricsAddress = "127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_ADDRCONNECTIVITY_PORT}";
        };

        websocket = {
          enable = true;
          websocketAddress = "127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_WEBSOCKET_PORT}";
        };

        alerts = {
          enable = true;
        };

        archiver = {
          enable = config.peer-observer.node.peer-observer.tools.archiver.enable;
          outputDir = "/data/peer-observer-archives/";
          baseName = "${config.peer-observer.node.peer-observer.tools.archiver.baseName}-${config.peer-observer.base.name}";
          compressionLevel = config.peer-observer.node.peer-observer.tools.archiver.compressionLevel;
        };
      };
    };

    systemd.services."bitcoind-banlist" =
      mkIf (config.peer-observer.node.bitcoind.banlistScript != null)
        {
          after = [ "bitcoind-mainnet.service" ];
          wantedBy = [ "multi-user.target" ];
          script = ''
            set -e
            shopt -s expand_aliases
            alias bitcoin-cli="${
              config.services.bitcoind."mainnet".package
            }/bin/bitcoin-cli -rpcuser=$RPC_BAN_USER -rpcpassword=$RPC_BAN_PW"
            echo "Banlist script started"
            echo "Waiting for bitcoind to come online"
            sleep 30


            echo "Currently banned IPs/Subnets:"
            bitcoin-cli listbanned
            echo ""

            echo "Clearing banned IPs/Subnets.."
            bitcoin-cli clearbanned
            echo ""

            echo "Banning IPs/Subnets.."
            ${config.peer-observer.node.bitcoind.banlistScript}

            echo "Now banned IPs/Subnets:"
            bitcoin-cli listbanned
            echo ""

            echo "Done"
          '';
          serviceConfig = {
            Type = "oneshot";
            Environment = "RPC_BAN_USER=ban RPC_BAN_PW=${CONSTANTS.BANLIST_RPC_PASSWORD}";
          };
        };
    systemd.tmpfiles.rules =
      optionals config.peer-observer.node.bitcoind.addrmanSnapshots.enable [
        "d ${CONSTANTS.ADDRMAN_SNAPSHOTS_DIR} 775 ${config.services.bitcoind.mainnet.user} ${config.services.bitcoind.mainnet.group} -"
      ]
      ++ optionals config.peer-observer.node.bitcoind.peersDatSnapshots.enable [
        "d ${CONSTANTS.PEERS_DAT_SNAPSHOTS_DIR} 775 ${config.services.bitcoind.mainnet.user} ${config.services.bitcoind.mainnet.group} -"
      ]
      ++ optionals config.peer-observer.node.bitcoind.peerinfoSnapshots.enable [
        "d ${CONSTANTS.PEERINFO_SNAPSHOTS_DIR} 775 ${config.services.bitcoind.mainnet.user} ${config.services.bitcoind.mainnet.group} -"
      ];

    systemd.services."addrman-snapshot" =
      mkIf config.peer-observer.node.bitcoind.addrmanSnapshots.enable
        {
          after = [ "bitcoind-mainnet.service" ];
          script = ''
            set -e
            shopt -s expand_aliases
            alias bitcoin-cli="${config.services.bitcoind.mainnet.package}/bin/bitcoin-cli -rpcuser=addrman-snapshot -rpcpassword=${CONSTANTS.ADDRMAN_SNAPSHOTS_RPC_PASSWORD}"
            DATE=$(date +%Y%m%d)
            SNAPSHOT="${CONSTANTS.ADDRMAN_SNAPSHOTS_DIR}/addrman-$DATE-${config.peer-observer.base.name}.json.zst"
            bitcoin-cli getrawaddrman | ${pkgs.zstd}/bin/zstd -19 -o "$SNAPSHOT"
            find ${CONSTANTS.ADDRMAN_SNAPSHOTS_DIR} -name "addrman-*.json.zst" \
              -mtime +${toString config.peer-observer.node.bitcoind.addrmanSnapshots.snapshotsToKeep} \
              -delete
          '';
          serviceConfig = {
            Type = "oneshot";
          };
        };

    systemd.timers."addrman-snapshot" =
      mkIf config.peer-observer.node.bitcoind.addrmanSnapshots.enable
        {
          wantedBy = [ "timers.target" ];
          timerConfig = {
            OnCalendar = "daily";
            Persistent = true;
          };
        };

    systemd.services."peers-dat-snapshot" =
      mkIf config.peer-observer.node.bitcoind.peersDatSnapshots.enable
        {
          after = [ "bitcoind-mainnet.service" ];
          script = ''
            set -e
            # Bitcoin Core writes peers.dat atomically (write to peers.dat.new,
            # then rename) every 15 minutes, so copying it here is safe. The
            # snapshot is at most 15 minutes stale.
            DATE=$(date +%Y%m%d)
            PEERS_DAT="${config.services.bitcoind.mainnet.dataDir}/${
              CONSTANTS.BITCOIND_DATADIR_SUBDIR_BY_CHAIN."${config.peer-observer.node.bitcoind.chain}"
            }peers.dat"
            SNAPSHOT="${CONSTANTS.PEERS_DAT_SNAPSHOTS_DIR}/peers-$DATE-${config.peer-observer.base.name}.dat.zst"
            # Read peers.dat via stdin (rather than passing it as a file
            # argument) so the compressed output gets default permissions
            # instead of inheriting peers.dat's restrictive 0600 mode, which
            # would stop nginx from serving it. The open fd also gives us a
            # consistent point-in-time read even if bitcoind rewrites the file.
            ${pkgs.zstd}/bin/zstd -19 -o "$SNAPSHOT" < "$PEERS_DAT"
            find ${CONSTANTS.PEERS_DAT_SNAPSHOTS_DIR} -name "peers-*.dat.zst" \
              -mtime +${toString (config.peer-observer.node.bitcoind.peersDatSnapshots.snapshotsToKeep * 7)} \
              -delete
          '';
          serviceConfig = {
            Type = "oneshot";
          };
        };

    systemd.timers."peers-dat-snapshot" =
      mkIf config.peer-observer.node.bitcoind.peersDatSnapshots.enable
        {
          wantedBy = [ "timers.target" ];
          timerConfig = {
            OnCalendar = "weekly";
            Persistent = true;
          };
        };

    systemd.services."peerinfo-snapshot" =
      mkIf config.peer-observer.node.bitcoind.peerinfoSnapshots.enable
        {
          after = [ "bitcoind-mainnet.service" ];
          script = ''
            set -e
            shopt -s expand_aliases
            alias bitcoin-cli="${config.services.bitcoind.mainnet.package}/bin/bitcoin-cli -rpcuser=peerinfo-snapshot -rpcpassword=${CONSTANTS.PEERINFO_SNAPSHOTS_RPC_PASSWORD}"
            # Unlike the addrman snapshots, these are taken multiple times a day,
            # so the timestamp needs to be more granular than a date. UTC, to not
            # depend on the node's timezone.
            DATE=$(date -u +%Y%m%dT%H%M%SZ)
            SNAPSHOT="${CONSTANTS.PEERINFO_SNAPSHOTS_DIR}/peerinfo-$DATE-${config.peer-observer.base.name}.json.zst"
            bitcoin-cli getpeerinfo | ${pkgs.zstd}/bin/zstd -19 -o "$SNAPSHOT"
            find ${CONSTANTS.PEERINFO_SNAPSHOTS_DIR} -name "peerinfo-*.json.zst" \
              -mtime +${toString config.peer-observer.node.bitcoind.peerinfoSnapshots.daysToKeep} \
              -delete
          '';
          serviceConfig = {
            Type = "oneshot";
          };
        };

    systemd.timers."peerinfo-snapshot" =
      mkIf config.peer-observer.node.bitcoind.peerinfoSnapshots.enable
        {
          wantedBy = [ "timers.target" ];
          timerConfig = {
            OnCalendar = "*-*-* 00,06,12,18:00:00";
            Persistent = true;
          };
        };

    services.bitcoind-profiling = mkIf config.peer-observer.node.samply-continuous-profiling {
      enable = true;
      package = config.peer-observer.base.b10c-pkgs.samply;
      nodeName = config.peer-observer.base.name;
    };

    services.nginx = {
      enable = true;
      recommendedGzipSettings = true;
      recommendedOptimisation = true;

      # This virtual host is exposed to the webserver(s). We use this to
      # explicitly limit what the webserver has access to on the node.
      virtualHosts."node-to-webserver-interface" = {
        listen = [
          {
            addr = "${config.peer-observer.base.wireguard.ip}";
            port = CONSTANTS.NODE_TO_WEBSERVER_PORT;
          }
        ];

        locations = {

          # access to the Bitcoin Core RPC interface
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_BITCOIND_RPC}" = {
            proxyPass = "http://127.0.0.1:${toString config.services.bitcoind.mainnet.rpc.port}";
            extraConfig = ''
              # Turn off some optimizations that don't work for some RPC clients.
              proxy_buffering on;
              proxy_request_buffering on;
              chunked_transfer_encoding off;
            '';
          };

          # access to debug logs the node hasn't deleted yet.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_DEBUG_LOGS}" = {
            alias = "${CONSTANTS.DEBUG_LOGS_DIR}/";
            extraConfig = ''
              autoindex on;
              autoindex_exact_size off;
              # Limit the download bandwidth here, so everyone accessing it via
              # the webserver is limited. This helps against a bandwidth DoS on the node.
              limit_rate 500k; # kB/s
            '';
          };

          # access to addrman snapshots the node hasn't deleted yet.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_ADDRMAN_SNAPSHOTS}" =
            mkIf config.peer-observer.node.bitcoind.addrmanSnapshots.enable
              {
                alias = "${CONSTANTS.ADDRMAN_SNAPSHOTS_DIR}/";
                extraConfig = ''
                  autoindex on;
                  autoindex_exact_size off;
                  limit_rate 500k; # kB/s
                '';
              };

          # access to peers.dat snapshots the node hasn't deleted yet.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEERS_DAT_SNAPSHOTS}" =
            mkIf config.peer-observer.node.bitcoind.peersDatSnapshots.enable
              {
                alias = "${CONSTANTS.PEERS_DAT_SNAPSHOTS_DIR}/";
                extraConfig = ''
                  autoindex on;
                  autoindex_exact_size off;
                  limit_rate 500k; # kB/s
                '';
              };

          # access to getpeerinfo snapshots the node hasn't deleted yet.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEERINFO_SNAPSHOTS}" =
            mkIf config.peer-observer.node.bitcoind.peerinfoSnapshots.enable
              {
                alias = "${CONSTANTS.PEERINFO_SNAPSHOTS_DIR}/";
                extraConfig = ''
                  autoindex on;
                  autoindex_exact_size off;
                  limit_rate 500k; # kB/s
                '';
              };

          # access to samply profile files the node hasn't deleted yet.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PROFILING}" =
            mkIf config.peer-observer.node.samply-continuous-profiling
              {
                alias = "${config.services.bitcoind-profiling.outputDir}/";
                extraConfig = ''
                  autoindex on;
                  autoindex_exact_size off;
                  limit_rate 500k; # kB/s
                '';
              };

          # access to the peer-observer websocket tool
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_WEBSOCKET_TOOL}" = {
            proxyPass = "http://127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_WEBSOCKET_PORT}/";
            proxyWebsockets = true;
          };

          # access to the peer-observer metrics tool
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_METRICS_TOOL}" = {
            proxyPass = "http://127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_METRICS_PORT}/metrics";
          };

          # access to the metrics by the peer-observer address connectivity tool
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_ADDRESSCONNECTIVITY_TOOL}" = {
            proxyPass = "http://127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_ADDRCONNECTIVITY_PORT}/metrics";
          };

          # access to the peer-observer rpc-extractor metrics
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_RPC_EXTRACTOR_METRICS}" = {
            proxyPass = "http://127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_RPC_METRICS_PORT}/metrics";
          };

          # access to the peer-observer ipc-extractor metrics
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_IPC_EXTRACTOR_METRICS}" = {
            proxyPass = "http://127.0.0.1:${toString CONSTANTS.PEER_OBSERVER_TOOL_IPC_METRICS_PORT}/metrics";
          };

          # access to the /metrics endpoint of the node-exporter tool.
          # Note: we don't want to give access to other paths on the node-exporter as they expose
          # sensitive information that we don't need to expose.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PROMETHEUS_EXPORTER_NODE}" = {
            proxyPass = "http://127.0.0.1:${toString config.services.prometheus.exporters.node.port}/metrics";
          };

          # access to the /metrics endpoint of the wireguard-exporter tool.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PROMETHEUS_EXPORTER_WIREGUARD}" = {
            proxyPass = "http://127.0.0.1:${toString config.services.prometheus.exporters.wireguard.port}/metrics";
          };

          # access to the /metrics endpoint of the process-exporter tool.
          "${CONSTANTS.NODE_TO_WEBSERVER_PATH_PROMETHEUS_EXPORTER_PROCESS}" = {
            proxyPass = "http://127.0.0.1:${toString config.services.prometheus.exporters.process.port}/metrics";
          };

        };
      };
    };

    # A process exporter for bitcoind. Exports the time spent in the different bitcoind threads.
    services.prometheus.exporters.process = {
      enable = true;
      settings.process_names = [
        # Remove nix store path from process name
        {
          comm = [ "bitcoind" ];
          name = "{{.Matches.Wrapped}} {{ .Matches.Args }}";
          cmdline = [ "^/nix/store[^ ]*/(?P<Wrapped>[^ /]*) (?P<Args>.*)" ];
        }
      ];
    };

    # The "root" user can use bitcoin-cli directly.
    programs.bash.shellAliases = {
      "bitcoin-cli" =
        "${config.services.bitcoind.mainnet.package}/bin/bitcoin-cli -rpccookiefile=${config.services.bitcoind.mainnet.dataDir}/.cookie";
    };

    environment.systemPackages = [
      # the peer-observer tools and extractors as packages
      # e.g. `$ logger`
      config.peer-observer.base.b10c-pkgs.peer-observer
    ];

  };

}
