{
  config,
  lib,
  pkgs,
  ...
}:
let
  CONSTANTS = import ../constants.nix;
  baseOptions = (import ../base/base.nix { inherit config lib pkgs; }).options;

  indexPageLimitedAccess = (
    pkgs.callPackage ./index.html.nix {
      inherit config;
      limited = true;
    }
  );
  indexPageFullAccess = (
    pkgs.callPackage ./index.html.nix {
      inherit config;
      limited = false;
    }
  );

  debugLogPage = (pkgs.callPackage ./debug-logs.html.nix) { inherit config; };

  # Combined overview page for both getrawaddrman and peers.dat snapshots.
  addrmanSnapshotsPage = (pkgs.callPackage ./addrman-snapshots.html.nix) { inherit config; };

  # Overview page for the getpeerinfo snapshots.
  peerinfoSnapshotsPage = (pkgs.callPackage ./peerinfo-snapshots.html.nix) { inherit config; };

  mkForkObserverNode = name: host: {
    inherit (host) id description;
    inherit name;
    rpcHost = (host.wireguard.ip);
    rpcPort = CONSTANTS.NODE_TO_WEBSERVER_PORT;
    rpcUser = "forkobserver";
    rpcPassword = CONSTANTS.FORK_OBSERVER_RPC_PASSWORD;
  };

  mkAddrmanObserverNode = name: host: {
    inherit (host) id;
    inherit name;
    rpc = {
      host = host.wireguard.ip;
      port = CONSTANTS.NODE_TO_WEBSERVER_PORT;
      user = "addrmanobserver";
      password = CONSTANTS.ADDRMAN_OBSERVER_RPC_PASSWORD;
    };
  };

  mkWebsocketJsonEntry = name: (''"${name}": "/websocket/${name}/"'');
  mkWebsocketJson = names: (lib.concatMapStringsSep ", " (name: (mkWebsocketJsonEntry name)) names);
  mkWebsocketLocation =
    name: host:
    (lib.nameValuePair ("/websocket/${name}/") ({
      proxyPass = "http://${host.wireguard.ip}:${toString CONSTANTS.NODE_TO_WEBSERVER_PORT}${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_WEBSOCKET_TOOL}";
      proxyWebsockets = true;
    }));

  mkDebugLogLocation =
    name: host:
    (lib.nameValuePair ("/debug-logs/${name}/") ({
      proxyPass = "http://${host.wireguard.ip}:${toString CONSTANTS.NODE_TO_WEBSERVER_PORT}${CONSTANTS.NODE_TO_WEBSERVER_PATH_DEBUG_LOGS}";
      extraConfig = ''
        limit_rate 500k; # kB/s
      '';
    }));

  # Only nodes with addrman snapshots enabled expose the snapshots endpoint.
  mkAddrmanSnapshotsLocation =
    name: host:
    (lib.nameValuePair ("/addrman-snapshots/${name}/") ({
      proxyPass = "http://${host.wireguard.ip}:${toString CONSTANTS.NODE_TO_WEBSERVER_PORT}${CONSTANTS.NODE_TO_WEBSERVER_PATH_ADDRMAN_SNAPSHOTS}";
      extraConfig = ''
        limit_rate 500k; # kB/s
      '';
    }));

  # Only nodes with peers.dat snapshots enabled expose the snapshots endpoint.
  mkPeersDatSnapshotsLocation =
    name: host:
    (lib.nameValuePair ("/peers-dat-snapshots/${name}/") ({
      proxyPass = "http://${host.wireguard.ip}:${toString CONSTANTS.NODE_TO_WEBSERVER_PORT}${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEERS_DAT_SNAPSHOTS}";
      extraConfig = ''
        limit_rate 500k; # kB/s
      '';
    }));

  # Only nodes with getpeerinfo snapshots enabled expose the snapshots endpoint.
  mkPeerinfoSnapshotsLocation =
    name: host:
    (lib.nameValuePair ("/peerinfo-snapshots/${name}/") ({
      proxyPass = "http://${host.wireguard.ip}:${toString CONSTANTS.NODE_TO_WEBSERVER_PORT}${CONSTANTS.NODE_TO_WEBSERVER_PATH_PEERINFO_SNAPSHOTS}";
      extraConfig = ''
        limit_rate 500k; # kB/s
      '';
    }));

  mkScrapeConfigs =
    hosts: port:
    (lib.mapAttrsToList (name: host: {
      targets = [ "${host.wireguard.ip}:${toString port}" ];
      labels = {
        host = name;
      };
    }) hosts);

in
{

  options = {
    enable = lib.mkEnableOption "this host is a web server";

    inherit (baseOptions)
      id
      name
      description
      wireguard
      setup
      arch
      extraConfig
      extraModules
      ;

    domain = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = null;
      example = "peer-observer.example.com";
      description = "The domain pointing to the IP address of this `web` host. This needs to be set.";
    };

    access_DANGER = lib.mkOption {
      type = lib.types.enum [
        CONSTANTS.NGINX_INTERNAL_LIMITED_ACCESS_NAME
        CONSTANTS.NGINX_INTERNAL_FULL_ACCESS_NAME
      ];
      default = CONSTANTS.NGINX_INTERNAL_LIMITED_ACCESS_NAME;
      example = CONSTANTS.NGINX_INTERNAL_FULL_ACCESS_NAME;
      description = ''
        Choose if the `${CONSTANTS.NGINX_INTERNAL_LIMITED_ACCESS_NAME}` or
        `${CONSTANTS.NGINX_INTERNAL_FULL_ACCESS_NAME}` peer-observer tools
        and data should be exposed. `${CONSTANTS.NGINX_INTERNAL_FULL_ACCESS_NAME}`
        is only intended for demo setups and SHOULD NOT be used for production
        setups. ${CONSTANTS.NGINX_INTERNAL_FULL_ACCESS_NAME} allows finding out
        the IP addresses of the honeypot nodes.
      '';
    };

    prometheus = {
      retention = lib.mkOption {
        type = lib.types.str;
        default = "30d";
        description = "How long the prometheus metrics should be kept.";
      };
    };

    grafana = {
      admin_user = lib.mkOption {
        type = lib.types.nullOr lib.types.str;
        default = null;
        description = "The username of the Grafana admin user.";
      };
    };

    index = {
      limitedAccessNotice = lib.mkOption {
        type = lib.types.str;
        default = "";
        description = "A notice to include at the top of the index.html page for LIMITED_ACCESS. Can contain HTML (styled with bootstrap).";
      };
      fullAccessNotice = lib.mkOption {
        type = lib.types.str;
        default = "";
        description = "A notice to include at the top of the index.html page for FULL_ACCESS. Can contain HTML (styled with bootstrap).";
      };
    };

    fork-observer = {
      networkName = lib.mkOption {
        type = lib.types.str;
        default = "mainnet";
        description = "Name of the chain / network of the peer-observer nodes.";
      };

      description = lib.mkOption {
        type = lib.types.str;
        default = "fork-observer attached to peer-observer nodes";
        description = "Description of the network and nodes.";
      };

      minForkHeight = lib.mkOption {
        type = lib.types.int;
        default = 500000;
        example = 0;
        description = "Height at which fork-observer should start to consider forks. The default works well for mainnet, but will not work for other chains/networks.";
      };

      poolIdentification = {
        enable = lib.mkEnableOption "enable pool-identification" // {
          default = true;
        };
        network = lib.mkOption {
          type = lib.types.str;
          default = "Mainnet";
          description = "Data and network used for pool-identification of the blocks.";
        };

      };
    };

  };

  config = lib.mkIf (config.peer-observer.web.enable && !config.peer-observer.base.setup) {

    assertions = [
      {
        assertion = config.peer-observer.web.domain != null;
        message = "`services.peer-observer-web.domain` must be set.";
      }
      {
        assertion = config.peer-observer.web.grafana.admin_user != null;
        message = "`services.peer-observer-web.grafana.admin_user` must be set.";
      }
    ];

    services.nginx = {
      enable = true;

      recommendedGzipSettings = true;
      recommendedOptimisation = true;
      recommendedProxySettings = true;
      recommendedTlsSettings = true;

      upstreams = {
        LIMITED_ACCESS.servers = {
          "127.0.0.1:${toString CONSTANTS.NGINX_INTERNAL_LIMITED_ACCESS_PORT}" = { };
        };
        FULL_ACCESS.servers = {
          "127.0.0.1:${toString CONSTANTS.NGINX_INTERNAL_FULL_ACCESS_PORT}" = { };
        };
      };

      appendHttpConfig = ''
        limit_req_zone $binary_remote_addr zone=ratelimit:10m rate=1r/s;
      '';

      virtualHosts = {
        # The internal endpoint that provides LIMITED peer-observer data.
        # The data accessible here is suitable for public vistors without
        # leaking information about the honeypot nodes.
        "LIMITED_ACCESS" = {
          listen = [
            {
              addr = "127.0.0.1";
              port = CONSTANTS.NGINX_INTERNAL_LIMITED_ACCESS_PORT;
            }
          ];
          locations = {
            "/" = {
              alias = "${indexPageLimitedAccess}/";
              extraConfig = ''
                default_type text/html;
              '';
            };

            "/forks" = {
              return = "301 /forks/";
            };
            "/forks/" = {
              proxyPass = "http://${config.services.fork-observer.address}/";
              proxyWebsockets = true;
              extraConfig = ''
                proxy_set_header Host $host;
                rewrite /forks/(.*) /$1  break;
              '';
            };
          };
        };

        # The internal endpoint that provides the FULL peer-observer data.
        # The data accessible here allows to find out the IP addresses of
        # the honeypot nodes. In a production setup, all access to this
        # should be restricted.
        "FULL_ACCESS" = {
          listen = [
            {
              addr = "127.0.0.1";
              port = CONSTANTS.NGINX_INTERNAL_FULL_ACCESS_PORT;
            }
          ];
          locations = {
            "/" = {
              alias = "${indexPageFullAccess}/";
              extraConfig = ''
                default_type text/html;
              '';
            };

            "/forks" = {
              extraConfig = "rewrite /forks /forks/ redirect;";
            };
            "/forks/" = {
              proxyPass = "http://${config.services.fork-observer.address}/";
              proxyWebsockets = true;
              extraConfig = ''
                proxy_set_header Host $host;
                rewrite /forks/(.*) /$1  break;
                add_header 'Access-Control-Allow-Origin' https://${config.peer-observer.web.domain};
              '';
            };

            "/monitoring" = {
              proxyPass = "http://127.0.0.1:${toString CONSTANTS.GRAFANA_PORT}";
              proxyWebsockets = true;
            };

            "/addrman" = {
              extraConfig = "rewrite /addrman /addrman/ redirect;";
            };
            "/addrman/" = {
              proxyPass = "http://${config.services.addrman-observer-proxy.address}/";
              extraConfig = ''
                proxy_set_header Host $host;
                rewrite /addrman/(.*) /$1  break;
              '';
            };

            "/debug-logs" = {
              extraConfig = "rewrite /debug-logs /debug-logs/ redirect;";
            };
            "/debug-logs/" = {
              root = "${debugLogPage}";
              index = "index.html";
              tryFiles = "$uri $uri/index.html $uri/ =404";
              extraConfig = ''
                default_type text/html;
                rewrite /debug-logs/(.*) /$1  break;
              '';
            };

            "/addrman-snapshots" = {
              extraConfig = "rewrite /addrman-snapshots /addrman-snapshots/ redirect;";
            };
            "/addrman-snapshots/" = {
              root = "${addrmanSnapshotsPage}";
              index = "index.html";
              tryFiles = "$uri $uri/index.html $uri/ =404";
              extraConfig = ''
                default_type text/html;
                rewrite /addrman-snapshots/(.*) /$1  break;
              '';
            };

            "/peerinfo-snapshots" = {
              extraConfig = "rewrite /peerinfo-snapshots /peerinfo-snapshots/ redirect;";
            };
            "/peerinfo-snapshots/" = {
              root = "${peerinfoSnapshotsPage}";
              index = "index.html";
              tryFiles = "$uri $uri/index.html $uri/ =404";
              extraConfig = ''
                default_type text/html;
                rewrite /peerinfo-snapshots/(.*) /$1  break;
              '';
            };

            "/websocket" = {
              extraConfig = "rewrite /websocket /websocket/ redirect;";
            };
            "/websocket/" = {
              root = config.peer-observer.base.b10c-pkgs.peer-observer.websocket-www-pages;
              index = "index.html";
              tryFiles = "$uri $uri/index.html $uri/ =404";
              extraConfig = ''
                autoindex on;
                default_type text/html;
                rewrite /websocket/(.*) /$1  break;
              '';
            };
            "/websocket/websockets.json".extraConfig = ''
              add_header Content-Type application/json;
              add_header Access-Control-Allow-Origin *;
              return 200 '{ ${mkWebsocketJson (lib.attrNames config.infra.nodes)} }';
            '';
          }
          // (lib.mapAttrs' mkWebsocketLocation (config.infra.nodes))
          // (lib.mapAttrs' mkDebugLogLocation (config.infra.nodes))
          // (lib.mapAttrs' mkAddrmanSnapshotsLocation (
            lib.filterAttrs (name: host: host.bitcoind.addrmanSnapshots.enable) config.infra.nodes
          ))
          // (lib.mapAttrs' mkPeersDatSnapshotsLocation (
            lib.filterAttrs (name: host: host.bitcoind.peersDatSnapshots.enable) config.infra.nodes
          ))
          // (lib.mapAttrs' mkPeerinfoSnapshotsLocation (
            lib.filterAttrs (name: host: host.bitcoind.peerinfoSnapshots.enable) config.infra.nodes
          ));
        };

        # Users can and should overwrite this, if they want to e.g. put FULL_ACCESS
        # behind authentification.
        "${config.peer-observer.web.domain}" = lib.mkDefault {
          enableACME = lib.mkDefault true;
          forceSSL = lib.mkDefault true;
          locations = lib.mkDefault {
            "/" = lib.mkDefault {
              proxyPass = "http://${config.peer-observer.web.access_DANGER}/";
              # needed for peer-observer websocket tool and Grafana!
              proxyWebsockets = true;
            };
          };
        };
      };
    };

    networking = {
      wireguard.interfaces.${CONSTANTS.WIREGUARD_INTERFACE_NAME} = {
        # the remainder of this interface is defined in base.nix

        # A web host is connected to all node hosts. The node hosts initiate
        # the wireguard connection to the web host, so node hosts don't
        # need to be reachable from the public internet. The Web host needs
        # to be reachable.
        listenPort = CONSTANTS.WIREGUARD_INTERFACE_PORT;
        peers = map (host: {
          allowedIPs = [ "${host.wireguard.ip}/32" ];
          publicKey = host.wireguard.pubkey;
          persistentKeepalive = 25;
        }) (lib.attrValues (lib.attrsets.filterAttrs (name: host: !host.setup) config.infra.nodes));
      };
      firewall = {
        # allow nginx ports on web hosts
        allowedTCPPorts = [
          80
          443
        ];
        # allow wireguard port on web hosts for nodes to connect to them
        allowedUDPPorts = [
          CONSTANTS.WIREGUARD_INTERFACE_PORT
        ];
      };
    };

    services.addrman-observer-proxy = {
      enable = true;
      address = "127.0.0.1:${toString CONSTANTS.ADDRMAN_OBSERVER_PORT}";
      nodes = lib.attrValues (lib.mapAttrs mkAddrmanObserverNode config.infra.nodes);
    };

    services.fork-observer = {
      enable = true;
      address = "127.0.0.1:${toString CONSTANTS.FORK_OBSERVER_PORT}";
      rss_base_url = "https://${config.peer-observer.web.domain}/forks";
      footer = ''
        <div class="my-2">
          <div>
            fork-observer attached to ${config.peer-observer.web.domain} nodes
          </div>
        </div>
      '';
      networks = [
        {
          id = 1;
          name = config.peer-observer.web.fork-observer.networkName;
          description = config.peer-observer.web.fork-observer.description;
          minForkHeight = config.peer-observer.web.fork-observer.minForkHeight;
          poolIdentification = {
            enable = config.peer-observer.web.fork-observer.poolIdentification.enable;
            network = config.peer-observer.web.fork-observer.poolIdentification.network;
          };
          nodes = lib.attrValues (lib.mapAttrs mkForkObserverNode config.infra.nodes);
        }
      ];
    };

    age.secrets.grafana-admin-password = {
      file = /${config.infra.agenixSecretsDir}/grafana-admin-password-${config.peer-observer.base.name}.age;
      mode = "0440";
      owner = config.users.users.grafana.name;
      group = config.users.users.grafana.group;
    };

    services.grafana-image-renderer = {
      enable = true;
      # We manually provision the grafana side.
      # (see "rendering" below)
      provisionGrafana = false;
      settings.server = {
        addr = "127.0.0.1:${toString CONSTANTS.GRAFANA_IMAGE_RENDERER_PORT}";
        auth-token = CONSTANTS.GRAFANA_IMAGE_RENDERER_AUTH_TOKEN;
      };
    };

    services.grafana = {
      enable = true;
      package = pkgs.grafana;
      settings = {
        server = {
          http_port = CONSTANTS.GRAFANA_PORT;
          http_addr = "127.0.0.1";
          serve_from_sub_path = true;
          enable_gzip = true;
          domain = config.peer-observer.web.domain;
          root_url = "https://${config.peer-observer.web.domain}/monitoring";
          enforce_domain = false;
        };
        security = {
          admin_password = "$__file{${config.age.secrets.grafana-admin-password.path}}";
          admin_user = config.peer-observer.web.grafana.admin_user;
          cookie_secure = true;
          # This hardcodes the old NixOS Grafana secret key, as we don't really have anything sentitive
          # in our database by default. YMMV.
          secret_key = "SW2YcwTIb9zpOOhoPsMm";
        };
        # https://github.com/grafana/grafana/issues/54974#issuecomment-1787591644
        rbac = {
          permission_validation_enabled = false;
        };
        "auth.anonymous" = {
          enabled = true;
          org_role = "Viewer";
        };
        analytics.reporting_enabled = false;
        "unified_alerting.screenshots" = {
          capture = true;
        };
        rendering = {
          # callback_url needs the "/monitoring" subpath!
          callback_url = "http://127.0.0.1:${toString CONSTANTS.GRAFANA_PORT}/monitoring";
          server_url = "http://${config.services.grafana-image-renderer.settings.server.addr}/render";
          renderer_token = CONSTANTS.GRAFANA_IMAGE_RENDERER_AUTH_TOKEN;
        };
      };
      provision = {
        enable = true;
        datasources.settings.datasources = [
          {
            name = "prometheus";
            type = "prometheus";
            url = "http://127.0.0.1:${toString config.services.prometheus.port}";
            isDefault = true;
          }
        ];
        dashboards.settings.providers = [
          {
            # dashboards shipped with the NixOS flake
            name = "peer-observer-flake";
            updateIntervalSeconds = 999999;
            options.path = (./dashboards);
            options.foldersFromFilesStructure = true;
          }
          {
            # dashboards shipped with the peer-observer metrics tool
            name = "peer-observer-metrics-tool";
            updateIntervalSeconds = 999999;
            options.path = config.peer-observer.base.b10c-pkgs.peer-observer.metrics-dashboards;
            options.foldersFromFilesStructure = true;
          }
          # TODO: allow to supply extra dashboards
        ];
      };
    };

    services.prometheus =
      let
        addrConnectivityNodes = lib.attrsets.filterAttrs (
          name: host: host.peer-observer.addrLookup
        ) config.infra.nodes;
      in
      {
        enable = true;
        retentionTime = config.peer-observer.web.prometheus.retention;
        # prometheus recording rules
        ruleFiles =
          lib.filter (f: lib.hasSuffix ".yaml" (toString f) || lib.hasSuffix ".yml" (toString f))
            (
              lib.filesystem.listFilesRecursive config.peer-observer.base.b10c-pkgs.peer-observer.metrics-prometheus-rules
            );
        scrapeConfigs = [
          # node scrape config:
          # on localhost
          {
            job_name = "node-local";
            scrape_interval = "15s";
            static_configs = [
              {
                # the config for the current web host
                targets = [ "127.0.0.1:${toString config.services.prometheus.exporters.node.port}" ];
                labels = {
                  host = config.peer-observer.base.name;
                };
              }
            ];
          }
          # on the node hosts
          {
            job_name = "node";
            scrape_interval = "15s";
            metrics_path = CONSTANTS.NODE_TO_WEBSERVER_PATH_PROMETHEUS_EXPORTER_NODE;
            static_configs = (mkScrapeConfigs config.infra.nodes CONSTANTS.NODE_TO_WEBSERVER_PORT);
          }
          # wireguard scrape config
          # on localhost:
          {
            job_name = "wireguard-localhost";
            scrape_interval = "30s";
            static_configs = [
              {
                # the config for the current web host
                targets = [ "127.0.0.1:${toString config.services.prometheus.exporters.wireguard.port}" ];
                labels = {
                  host = config.peer-observer.base.name;
                };
              }
            ];
          }
          # on the node hosts:
          {
            job_name = "wireguard";
            scrape_interval = "30s";
            metrics_path = CONSTANTS.NODE_TO_WEBSERVER_PATH_PROMETHEUS_EXPORTER_WIREGUARD;
            static_configs = (mkScrapeConfigs config.infra.nodes CONSTANTS.NODE_TO_WEBSERVER_PORT);
          }
          # peer-observer-metrics scrape config
          {
            job_name = "peer-observer-metrics";
            fallback_scrape_protocol = "PrometheusText0.0.4";
            scrape_interval = "15s";
            scrape_timeout = "14s";
            metrics_path = CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_METRICS_TOOL;
            static_configs = (mkScrapeConfigs config.infra.nodes CONSTANTS.NODE_TO_WEBSERVER_PORT);
          }
          # addrConnectivityLookup scrape config
          {
            job_name = "peer-observer-addr-connectivity";
            scrape_interval = "15s";
            fallback_scrape_protocol = "PrometheusText0.0.4";
            metrics_path = CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_ADDRESSCONNECTIVITY_TOOL;
            static_configs = (mkScrapeConfigs addrConnectivityNodes CONSTANTS.NODE_TO_WEBSERVER_PORT);
          }
          # rpc-extractor metrics scrape config
          {
            job_name = "peer-observer-rpc-extractor-metrics";
            scrape_interval = "30s";
            fallback_scrape_protocol = "PrometheusText0.0.4";
            metrics_path = CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_RPC_EXTRACTOR_METRICS;
            static_configs = (mkScrapeConfigs config.infra.nodes CONSTANTS.NODE_TO_WEBSERVER_PORT);
          }
          # ipc-extractor metrics scrape config
          {
            job_name = "peer-observer-ipc-extractor-metrics";
            scrape_interval = "30s";
            fallback_scrape_protocol = "PrometheusText0.0.4";
            metrics_path = CONSTANTS.NODE_TO_WEBSERVER_PATH_PEER_OBSERVER_IPC_EXTRACTOR_METRICS;
            static_configs = (mkScrapeConfigs config.infra.nodes CONSTANTS.NODE_TO_WEBSERVER_PORT);
          }
          # bitcoind process exporter scrape config
          {
            job_name = "process-exporter";
            scrape_interval = "30s";
            metrics_path = CONSTANTS.NODE_TO_WEBSERVER_PATH_PROMETHEUS_EXPORTER_PROCESS;
            static_configs = (mkScrapeConfigs config.infra.nodes CONSTANTS.NODE_TO_WEBSERVER_PORT);
          }
        ];
      };
  };
}
